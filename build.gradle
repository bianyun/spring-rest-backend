buildscript {
    ext {
        springBootVersion = '2.4.1'
        mapstructVersion = '1.4.1.Final'
        hutoolVersion = '5.5.6'
        swaggerAnnotationsVersion = '1.5.22'
        knife4jVersion = '2.0.8'
        jpaSpecVersion = '3.2.5'
        shiroVersion = '1.7.0'
        jooqMetaExtensionsVersion = '3.14.4'
        antrl4Version = '4.8-1'
        googleJsr305Version = '3.0.2'
    }
}

plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'java'
}

allprojects {
    apply plugin: 'io.spring.dependency-management'

    group = 'com.silentcloud'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '1.8'

    repositories {
        maven {
            url 'https://maven.aliyun.com/repository/public'
        }
        jcenter()
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
//        options.compilerArgs = ['-Xlint']
    }
}

subprojects {
    apply plugin: 'java'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        annotationProcessor platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation "cn.hutool:hutool-all:${hutoolVersion}"
        implementation "com.google.code.findbugs:jsr305:${googleJsr305Version}"

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    test {
        useJUnitPlatform()
    }

}
